apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def versionName = '1.0.0'

android {
    compileSdkVersion 27


    defaultConfig {
        minSdkVersion 27
        targetSdkVersion 27
        versionCode 1

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    compileOnly 'com.google.android.things:androidthings:+'

}

def siteUrl = 'https://github.com/plattysoft/PlattyThings/tree/master/mpu6050'
def gitUrl = 'https://github.com/plattysoft/PlattyThings.git'

group = "com.plattysoft.things"
version = versionName

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "PlattyThings"
        name = "MPU6050"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        dryRun = false
        publish = true
        version {
            name = versionName //Bintray logical version name
            vcsTag = versionName
        }
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                artifactId 'mpu6050'
                version = versionName
                groupId = group
                // Add your description here
                name 'MPU6050'
                description = 'Gyro / Accelerometer MPU6050 driver for Android Things'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'plattysoft'
                        name 'Platty Soft'
                        email 'raul@plattysoft.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
    archiveName "mpu6050-${versionName}-sources.jar"
}

artifacts {
    archives sourcesJar
}
repositories {
    mavenCentral()
}
