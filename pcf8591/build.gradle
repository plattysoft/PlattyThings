apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def versionName = '0.8'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 27
        targetSdkVersion 27
        versionCode 1

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def siteUrl = 'https://github.com/plattysoft/PlattyThings/tree/master/pcf8591'
def gitUrl = 'https://github.com/plattysoft/PlattyThings.git'
def artifact = "pcf8591"
def group = "com.plattysoft.things"
version = versionName

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compileOnly 'com.google.android.things:androidthings:+'

    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
    archiveName "pcf8591-${versionName}-sources.jar"
}

bintray {

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

//    publications = ['Production']
    configurations = ['archives']
    pkg {
        repo = "PlattyThings"
        name = artifact
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        dryRun = false
        publish = true
        version {
            name = versionName //Bintray logical version name
            vcsTag = versionName
        }
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                artifactId artifact
                version = versionName
                groupId = group
                // Add your description here
                name artifact
                description = 'ADC PCF8591 driver for Android Things'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'plattysoft'
                        name 'Platty Soft'
                        email 'raul@plattysoft.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task createPom {
    apply plugin: 'maven'
    description "Generates pom.xml"
    pom {
        project {
            groupId group
            artifactId artifact
            version versionName
            packaging 'aar'
        }
    }.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')

        configurations.compile.allDependencies.each { dependency ->
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', dependency.group)
            dependencyNode.appendNode('artifactId', dependency.name)
            dependencyNode.appendNode('version', dependency.version)
        }
    }.writeTo("$buildDir/poms/pcf8591-${versionName}.pom")
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenCentral()
}

bintrayUpload.dependsOn(assemble, sourcesJar, createPom)