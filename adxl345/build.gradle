apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def versionName = '1.0.2'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 27
        versionCode 1

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    compileOnly 'com.google.android.things:androidthings:+'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

def siteUrl = 'https://github.com/plattysoft/PlattyThings/tree/master/adxl345'
def gitUrl = 'https://github.com/plattysoft/PlattyThings.git'

group = "com.plattysoft.things"
version = versionName

def groupPackageId = group

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "PlattyThings"
        name = "ADXL345"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        dryRun = false
        publish = true
        version {
            name = versionName //Bintray logical version name
            vcsTag = versionName
        }
    }
}

task createPom {
    apply plugin: 'maven'
    description "Generates pom.xml"
    pom {
        project {
            packaging 'aar'
            artifactId 'adxl345'
            version = versionName
            groupId = groupPackageId
            name 'adxl345'.toUpperCase()
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'plattysoft'
                    name 'Platty Soft'
                    email 'raul@plattysoft.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')

        configurations.compile.allDependencies.each { dependency ->
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', dependency.group)
            dependencyNode.appendNode('artifactId', dependency.name)
            dependencyNode.appendNode('version', dependency.version)
        }
    }.writeTo("$buildDir/poms/pom-default.xml")
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
    archiveName "adxl345-${versionName}-sources.jar"
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenCentral()
}

bintrayUpload.dependsOn(assemble, sourcesJar, createPom)