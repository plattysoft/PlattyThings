apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def versionName = '0.8.1'

android {
    compileSdkVersion 27


    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 27
        versionCode 1

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    compileOnly 'com.google.android.things:androidthings:+'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

}

def siteUrl = 'https://github.com/plattysoft/PlattyThings/tree/master/pcf8575'
def gitUrl = 'https://github.com/plattysoft/PlattyThings.git'
group = "com.plattysoft.things"
version = versionName

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "PlattyThings"
        name = "PCF8575"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        dryRun = false
        publish = true
        version {
            name = versionName //Bintray logical version name
            vcsTag = versionName
        }
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                artifactId 'pcf8575'
                version = versionName
                groupId = group
                // Add your description here
                name 'pcf8575'
                description = 'GPIO Extension PCF8575 driver for Android Things'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'plattysoft'
                        name 'Platty Soft'
                        email 'raul@plattysoft.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
    archiveName "pcf8575-${versionName}-sources.jar"
}

artifacts {
    archives sourcesJar
}
repositories {
    mavenCentral()
}